// ============================================================================
// COMPOSITE HUNT (L2 / L2.5): Scheduled Task Abuse (Creation / Modification)
// AUTHOR: Ala Dabat
// MITRE: T1053.005 (Scheduled Task/Job: Scheduled Task)
// DATA: DeviceProcessEvents (MDE / Sentinel)
// ---------------------------------------------------------------------------
// WHY COMPOSITE (Framework):
//   Baseline Truth  : A task is created/changed (schtasks.exe /create or /change)
//   Reinforcement   : Task action is a script engine OR points to a user-writable path
//   Noise Control   : Suppress obvious maintenance/updater patterns and Program Files paths
//   Pivot: If scheduled task runs COM to registry to launch a malicious DLL my registry persistence rule will catch it
// 1. Minimum baseline of truth is a non-negotiable withoout it there is no rule. A noisey rule is a defect in rule logic
// The minimum baseline of truth is: schtasks.exe running /create or /change someone created a scheduled task, thousands of google updates and SCCM create these re-enforcment is required
// Creating a shceduled task is the minim truth of the event, and not the attack.
// 2. Convergence collapses when a scheduled task spawns a malicious process... from a user writable path!
// Attack Truth is not Task Creation," but as "Task Creation + Dangerous Payload + Loaded from user writable path." This moves the rule from "Noise" to "Signal.
// 3. Operational reality suppress obvious maintenance/updater patterns is a requirment for tuning out noise and convergence on truth ensures low noise to signal.
//4. Analyst a Playbook in a String tells analysts exactly what fields matter (TaskName, TaskRun) and what to do (Verify owner). This reduces "Time to Decision." ensures operational integrity.
// NOTES:
//   - This is built for operating reality: tasks are common, but "who/what/where" is not.
//   - You should tune allowlists per tenant (SCCM, OEM updaters, EDR agents).
// ============================================================================

let Lookback = 7d;

// User-writable / commonly abused execution paths (reinforcement)
let WritablePaths = dynamic([
  "\\users\\public\\",
  "\\programdata\\",
  "\\windows\\temp\\",
  "\\temp\\",
  "\\appdata\\local\\temp\\",
  "\\appdata\\roaming\\",
  "\\perflogs\\"
]);

// Script/proxy engines commonly launched from task actions (reinforcement)
let ScriptEngines = dynamic([
  "powershell.exe","pwsh.exe","cmd.exe","wscript.exe","cscript.exe","mshta.exe","rundll32.exe","regsvr32.exe" //scheduled tasks launching these from public writable paths is where convergence ends and malicious intent begins
]);

// Tenant tuning: common benign task creators (examples; add your own)
let AllowInitiatingParents = dynamic([
  "sihclient.exe",          // Windows servicing
  "usoclient.exe",          // Windows Update
  "trustedinstaller.exe",
  "tiworker.exe",
  "msiexec.exe",
  "setuphost.exe"
]);

// Tenant tuning: common benign keywords in task cmdlines (examples; add your own)
let AllowKeywords = dynamic([
  "\\microsoft\\windows\\", // common built-in task paths
  "onedrive", "teams", "edgeupdate", "googleupdate",
  "sccm", "ccm", "intune", "defender", "microsoft update"
]);

DeviceProcessEvents
| where Timestamp >= ago(Lookback)
| where FileName =~ "schtasks.exe"
| extend Cmd = tolower(ProcessCommandLine)
| where Cmd has_any ("/create", "/change")    // Minimum truth a user creates a scheduled task not, malicious or taht suspicious yet but is a non negotionable starting point for this LOLBIN
                                              // Adobe, Google, and other updates create thousands of scheduled tasks across an enterprise environment. 
// Reinforcement extraction
//
| extend
    HasTaskAction = iif(Cmd has "/tr" or Cmd has " /tr ", 1, 0),
    UsesScriptEngine = iif(Cmd has_any (ScriptEngines), 1, 0),  //this ties into the end of convergence and the start of malicious intent as suspicious script engines launching from user writable paths have a high malicious probability rate
    UsesWritablePath  = iif(Cmd has_any (WritablePaths), 1, 0),  //Convergence collapses when a scheduled task spawns a malicious process like powershell or other binaries from a user writable path, this is high convergence and malicious intent
    RunsAsSystem      = iif(Cmd has_any ("/ru system"," /ru \"system\""," /ru \"nt authority\\system\"","/ru \"nt authority\\system\""), 1, 0), // Running suspicious flags or with elevated privilages gives hunters a clear indicators to investigate
    SuspSchedule      = iif(Cmd has_any ("/sc onlogon","/sc onstart","/sc minute","/sc hourly") or Cmd has "/mo", 1, 0), //re-enforced telemetry showing intent as part of a re-enforced cumulative score increases cetainty while reducing noise
    HiddenTaskHints   = iif(Cmd has_any ("/f","/rl highest","/it","/z"), 1, 0)
//
// Minimum Truth gate + reinforcement (composite)
//   - schtasks /create|/change is baseline
//   - must have script engine OR writable path (else too noisy)
//
| where UsesScriptEngine == 1 or UsesWritablePath == 1
//
// Noise suppression (avoid obvious benign patterns)
//
| where not(tolower(InitiatingProcessFileName) in~ (AllowInitiatingParents))
| where not(Cmd has_any (AllowKeywords))
| where not(Cmd has "\\program files\\")  // task action pointing into PF is often legit (tune if needed)
//
// Score (simple + explainable)
//
| extend RiskScore =
      0
    + iif(HasTaskAction == 1, 15, 0)
    + iif(UsesScriptEngine == 1, 35, 0)
    + iif(UsesWritablePath  == 1, 35, 0)
    + iif(RunsAsSystem      == 1, 20, 0)
    + iif(SuspSchedule      == 1, 15, 0)
    + iif(HiddenTaskHints   == 1, 10, 0)
| where RiskScore >= 40
| extend Severity = case(
    RiskScore >= 85, "CRITICAL",
    RiskScore >= 65, "HIGH",
    "MEDIUM"
)
| extend HunterDirective = strcat(
    "ACTION: ", Severity, " scheduled task persistence triage | ",
    "CONTEXT: schtasks /create|/change with ",
      iif(UsesScriptEngine==1, "ScriptEngine", "NonScript"),
      " + ",
      iif(UsesWritablePath==1, "WritablePath", "NonWritablePath"),
      iif(RunsAsSystem==1, " + SYSTEM", ""),
      " | ",
    "PIVOT: extract TaskName (/tn) + TaskRun (/tr). Verify owner/change ticket. ",
    "If unapproved: inspect referenced payload, check recent downloads, and hunt for lateral movement."
)
| project
    Timestamp,
    DeviceName,
    AccountName,
    InitiatingProcessFileName,
    FileName,
    RiskScore,
    Severity,
    HunterDirective,
    ProcessCommandLine
| order by RiskScore desc, Timestamp desc
